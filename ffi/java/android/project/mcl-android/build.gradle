plugins {
	id 'com.android.library'
	id 'maven-publish'
	id 'com.jfrog.bintray'
}

android {
	compileSdkVersion 30
	buildToolsVersion "29.0.3"

	defaultConfig {
		minSdkVersion 16
		targetSdkVersion 30
		versionCode 1
		versionName "0.3"

		testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
	}

	buildTypes {
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
		}
	}
	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}

	externalNativeBuild {
		ndkBuild {
			path '../../../android/jni/Android.mk'
		}
	}
	sourceSets {
		main {
			java {
				srcDirs = ['../../../../java/com/herumi/mcl']
			}
		}
	}

}

task androidSourcesJar(type: Jar) {
	archiveClassifier = 'sources'
	from android.sourceSets.main.java.srcDirs
}
afterEvaluate {
	publishing {
		publications {
			"sdkRelease"(MavenPublication) {
				from components.release
				groupId 'ch.ubique.mcl'
				artifactId 'mcl-android'
				version android.defaultConfig.versionName + readPropertyWithDefault('bintrayVersionSuffix', '')
				artifact androidSourcesJar
			}
		}
	}
}

ext.readProperty = { paramName -> readPropertyWithDefault(paramName, null) }
ext.readPropertyWithDefault = { paramName, defaultValue ->
	if (project.hasProperty(paramName)) {
		return project.getProperties().get(paramName)
	} else {
		Properties properties = new Properties()
		if (project.rootProject.file('local.properties').exists()) {
			properties.load(project.rootProject.file('local.properties').newDataInputStream())
		}
		if (properties.getProperty(paramName) != null) {
			return properties.getProperty(paramName)
		} else {
			return defaultValue
		}
	}
}

bintray {
	user = readProperty('bintrayUser')
	key = readProperty('bintrayApikey')
	publications = ['sdkRelease']
	publish = true
	pkg {
		repo = 'mcl-android'
		name = 'mcl-android'
		userOrg = 'mcl-android'
		licenses = ['MPL-2.0']
	}
}

dependencies {

	implementation 'androidx.appcompat:appcompat:1.2.0'
	implementation 'com.google.android.material:material:1.2.1'
	testImplementation 'junit:junit:4.+'
	androidTestImplementation 'androidx.test.ext:junit:1.1.2'
	androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}